# Maintainer: Kowabunga
# Contributor: Kowabunga
# Contributor: yochananmarqos
# Contributor: bpierre
# Contributor: PedroHLC
# Contributor: rodrigo21

pkgname='vkpost'
pkgver=0.5.1
pkgrel=2
pkgdesc='A Vulkan Post Processing Layer For Linux. Next Gen vkBasalt.'
arch=('x86_64')
url='https://github.com/KowabungaOfficial/vkPost'
license=('Zlib')
depends=('glibc' 'gcc-libs' 'libx11' 'wayland' 'libxkbcommon')
makedepends=('glslang' 'meson' 'ninja' 'spirv-headers' 'vulkan-headers')
optdepends=('reshade-shaders-git: collection of shaders to use with vkPost')
source=("${url}/releases/download/v${pkgver}/vkPost-${pkgver}.tar.gz")
b2sums=('60d7034fedeb29a04e887f8bdbf9ec3ead25829506647831c7830efe8efbb26a0bad2cdd0c4042bfca164599cd234e78d541cbdff51a9dcb08f491d6fb3488d9')
install=vkpost.install

prepare() {
  cd "${srcdir}/vkPost-${pkgver}"
  sed -i 's|/path/to/reshade-shaders/Textures|/opt/reshade/textures|g' \
    "config/vkPost.conf"
  sed -i 's|/path/to/reshade-shaders/Shaders|/opt/reshade/shaders|g' \
    "config/vkPost.conf"
}

build() {
  cd "${srcdir}/vkPost-${pkgver}"

  arch-meson \
    --buildtype=release \
    build
  ninja -C build
}

package() {
  cd "${srcdir}/vkPost-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C build install
  mv "${pkgdir}/usr/share/vulkan/implicit_layer.d/vkPost.json" "${pkgdir}/usr/share/vulkan/implicit_layer.d/vkPost.x86_64.json"
  install -Dm 644 config/vkPost.conf "${pkgdir}/usr/share/vkPost/vkPost.conf.example"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
